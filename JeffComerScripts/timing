#!/bin/tcsh

if ( $# != 1 && $# != 2 ) then
    echo "Usage: timing [-c] filename"
    echo "  Options:"
    echo "    -c    Use CPU time instead of walltime."
    exit -1
endif

if ( $# == 1 ) then
    set WALL = 1
    set FILE = $1
else
    if ( "$1" == "-c" ) then
	set WALL = 0
	set FILE = $2
    else
	echo "Usage: timing [-c] filename"
	echo "  Options:"
	echo "    -c    Use CPU time instead of walltime."
	exit -1
    endif
endif
	
set PROCS = `egrep 'Running on [0-9]* processors' $FILE | awk '{print $4}'`
set ATOMS = `egrep 'Info: [0-9]* ATOMS' $FILE | awk '{print $2}'`
set TS =    `egrep 'Info: TIMESTEP[ \t]+[0-9]*' $FILE | awk '{print $3}'`
echo $PROCS "processors"
echo $ATOMS "atoms"

if ( $WALL ) then
    grep "^TIMING" $FILE | awk '{n++;t+=$8};END{printf("<sec/step> = %g  <steps/proc*sec> = %g\n<atoms*steps/proc*sec> = %g  <Mans/SU> = %e  <SU/Mans> = %d\n", t/n, n/(p*t), a*n/(p*t), (a/1e6)*(n/1e6)/(p*t/ts/3600), (p*t/ts/3600)/((a/1e6)*(n/1e6)))}' p=$PROCS a=$ATOMS ts=$TS
    grep "^TIMING" $FILE | tail -n 1 | awk '{printf("steps = %d  walltime = %d sec (%.2f hrs)  steps/hr = %.1f\n", $2, $7, $7/3600, $2*3600/$7)}'
else
    grep "^TIMING" $FILE | awk '{n++;t+=$5};END{printf("<sec/step> = %g  <steps/proc*sec> = %g\n<atoms*steps/proc*sec> = %g  <Mans/SU> = %e  <SU/Mans> = %d\n", t/n, n/(p*t), a*n/(p*t), (a/1e6)*(n/1e6)/(p*t/ts/3600), (p*t/ts/3600)/((a/1e6)*(n/1e6)))}' p=$PROCS a=$ATOMS ts=$TS
    grep "^TIMING" $FILE | tail -n 1 | awk '{printf("steps = %d  cputime = %d sec (%.2f hrs)  steps/hr = %.1f\n", $2, $4, $4/3600, $2*3600/$4)}'
endif
