# potentialProfileAverage.tcl
# Average the potential profile in slices in the xy-plane, obtaining V(z).
# Requires gridForce.tcl.
# Author: Jeff Comer <jcomer2@illinois.edu>

if {$argc != 1} {
	puts "This script requires one argument: "
	puts "  file name"
	puts ""
	exit
}

proc trimExtension {name} {
    set ind [string last "." $name]
    return [string range $name 0 [expr $ind-1]]
}

set filePrefix [trimExtension [lindex $argv 0]]
# Input:
set inFile ${filePrefix}.dx
# Output:
set outFile ${filePrefix}_avgz.dat

source vector.tcl
source gridForce.tcl

readDx grid $inFile
puts "Read $inFile, containing [llength $grid(data)] potential values."
puts "Size: $grid(nx) $grid(ny) $grid(nz)"

puts "Writing potential profile of $grid(nz) points to $outFile..."
set out [open $outFile w]
for {set iz 0} {$iz < $grid(nz)} {incr iz} {
    set r [getPosition grid [list 0 0 $iz]]
    set z [lindex $r 2]
    set pot 0.0

    # Compute the average potential in this slice.
    for {set iy 0} {$iy < $grid(ny)} {incr iy} {
	for {set ix 0} {$ix < $grid(nx)} {incr ix} {
	    set i [expr $iz + $grid(nz)*$iy + $grid(nz)*$grid(ny)*$ix]
	    set pot [expr $pot + [lindex $grid(data) $i]]
	}
    }
   
    set pot [expr $pot/($grid(nx)*$grid(ny))]
    puts $out "$z $pot"
}
close $out
puts "Done."



